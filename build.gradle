buildscript {
    ext.kotlin_version = '1.4.21'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.18"
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.8.5'
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'signing'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile 'junit:junit:4.13.1'
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

jar {
    archivesBaseName = 'npy'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

signing {
    // multiline environment variables are not fun.
    def signingKey = findProperty("signingKey")?.replace("\\n", "\n")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign configurations.archives
}

publishing {
    group = 'org.jetbrains.bio'
    version = project.version

    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'npy'
        userOrg = "jetbrains-research"
        licenses = ['MIT']
        version {
            name = project.version
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            def ossrhUsername = findProperty("ossrhUsername")
            def ossrhPassword = findProperty("ossrhPassword")

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.groupId = 'org.jetbrains.bio'

            pom.project {
                name 'npy'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'NPY and NPZ support for the JVM'
                url 'https://github.com/JetBrains-Research/npy'

                scm {
                    connection 'scm:git:git@github.com:JetBrains-Research/npy.git'
                    developerConnection 'scm:git:git@github.com:JetBrains-Research/npy.git'
                    url 'https://github.com/JetBrains-Research/npy'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/JetBrains-Research/npy/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'dievsky'
                        name 'Aleksei Dievskii'
                        email 'alexey.dievsky@jetbrains.com'
                    }
                }
            }
        }
    }
}

wrapper {
    gradleVersion = '6.5'
}
