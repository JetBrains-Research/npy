buildscript {
    ext.kotlin_version = '1.4.21'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.18"
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.6'
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
}

apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile 'junit:junit:4.13.1'
}

dokka {
    moduleName = "$version"
    outputFormat = 'gfm'
    outputDirectory = "$rootDir/docs"

    linkMapping {
        dir = 'src/main/kotlin'
        url = "https://github.com/JetBrains-Research/npy/blob/${version}/src/main/kotlin"
        suffix = '#L'
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    group = 'org.jetbrains.bio'
    version = project.version

    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'npy'
        userOrg = "jetbrains-research"
        licenses = ['MIT']
        version {
            name = project.version.toString()
        }
    }
}

wrapper {
    gradleVersion = '6.5'
}
